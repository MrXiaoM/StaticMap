plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

allprojects {
    group = 'com.molean'
    version = '1.2.3'

    repositories {
        maven { url "https://mirrors.huaweicloud.com/repository/maven" }
        maven { url "https://repo.papermc.io/repository/maven-public/" }
        maven { url "https://mvn.lumine.io/repository/maven/" }
        maven { url "https://repo.codemc.io/repository/maven-public/" }
        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        mavenCentral()
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        sourceCompatibility = 8
        targetCompatibility = 8
    }
}
dependencies {
    compileOnly("com.destroystokyo.paper:paper-api:1.14-R0.1-SNAPSHOT")
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.11.1'
    implementation 'org.jetbrains:annotations:23.0.0'
    for (Project proj : project(":nms").subprojects) {
        implementation(proj)
    }
}

shadowJar {
    archiveClassifier = ""
    relocate("org.intellij.lang.annotations", "com.molean.staticmap.utils.annorations.intellij")
    relocate("org.jetbrains.annotations", "com.molean.staticmap.utils.annorations.jetbrains")
}
tasks.build.dependsOn(shadowJar)

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from("LICENSE")
    from(sourceSets.main.resources.srcDirs) {
        expand('version': project.version)
        include 'plugin.yml'
    }
}
